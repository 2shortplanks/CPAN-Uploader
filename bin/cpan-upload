#!perl
use strict;
use warnings;
use Carp;
use CPAN::Uploader;
use File::Spec;

use Getopt::Long::Descriptive;
$Getopt::Long::Descriptive::MungeOptions = 1;

my %arg;

my $home = $ENV{HOME} || '.';
my $rc   = File::Spec->catfile($home, '.pause');

# Process .pause
open my $pauserc, '<', $rc or die "can't open $rc for reading: $!";

while (<$pauserc>) {
  chomp;
  next unless $_ and $_ !~ /^\s*#/;

  my ($k, $v) = /^\s*(\w+)\s+(.+)$/;
  croak "multiple enties for $k" if $arg{$k};
  $arg{$k} = $v;
}

# Process arguments
my ($opts, $usage) = describe_options(
	"usage: %c [options] file-to-upload",
	( 
		[ "verbose|v" => "Verbose logging" ],
		[ "help|h"    => "This help message" ],
		[ "dry-run"   => "Do not upload anything." ], 
		[],
		[ "user|u=s"    => "Your PAUSE username (which was previously registered with PAUSE" ],
		[ "password|p=s" => "The password to your PAUSE account" ],
		[ "directory|d=s" => "A subdirectory in your CPAN area where the file should be uploaded to" ],
		[ "http-proxy=s" => "URL of the http proxy to use in uploading" ],
	),
);

die $usage if $opts->{help};
die "Please provide a file name.\n".$usage unless my $file = $ARGV[0];

$arg{debug} = 1 if $opts->{verbose};
$arg{subdir} = $opts->{directory} if defined $opts->{directory};

map { $arg{$_} = $opts->{$_} if defined $opts->{$_} } qw(dry_run user password http-proxy);


CPAN::Uploader->upload_file(
  $file,
  \%arg,
);
